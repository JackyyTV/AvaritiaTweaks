buildscript {
  repositories {
    jcenter()
    maven { url = "http://files.minecraftforge.net/maven" }
  }
  dependencies {
    classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
  }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "${mc_version}-${mod_version}"
group = "jackyy.avaritiatweaks"
archivesBaseName = "AvaritiaTweaks"

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
  sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
  version = "${forge_version}"
  runDir = "run"
  mappings = "${mappings_version}"

  replace '@FINGERPRINT@', project.findProperty('signSHA1')
  replaceIn "AvaritiaTweaks.java"
}

repositories {
  maven {
    name = "JEI Maven"
    url = "https://dvs1.progwml6.com/files/maven"
  }
  maven {
    name = "CB Maven"
    url = "http://chickenbones.net/maven"
  }
  maven {
    name = "CoFH Maven"
    url = "http://maven.covers1624.net"
  }
  maven {
    name = "Curse Maven"
    url = "https://www.cursemaven.com"
  }
}

dependencies {
  deobfCompile "mezz.jei:jei_1.11.2:+"
  deobfCompile "curse.maven:hwyla:2496989" //1.8.22-B37_1.11
  deobfCompile "codechicken:CodeChickenLib:1.11.2-+:universal"
  deobfCompile("morph.avaritia:Avaritia:1.11.2-+:universal") {
      transitive = false
  }
  deobfCompile "curse.maven:botania:2451047" //r1.9-345
}

processResources {
  inputs.property "version", project.version
  inputs.property "mcversion", project.minecraft.version
  from(sourceSets.main.resources.srcDirs) {
    include 'mcmod.info'
    expand 'version':project.version, 'mcversion':project.minecraft.version
  }
  from(sourceSets.main.resources.srcDirs) {
    exclude 'mcmod.info'
  }
  rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
  manifest {
    attributes 'FMLAT': 'avaritiatweaks_at.cfg'
  }
}

task signJar(type: SignJar, dependsOn: reobfJar) {
  onlyIf {
    project.hasProperty('keyStore')
  }
  keyStore = project.findProperty('keyStore')
  alias = project.findProperty('keyStoreAlias')
  storePass = project.findProperty('keyStorePass')
  keyPass = project.findProperty('keyStoreKeyPass')
  inputFile = jar.archivePath
  outputFile = jar.archivePath
}

build.dependsOn signJar

task deobfJar(type: Jar) {
  from sourceSets.main.output
  classifier = 'deobf'
}

artifacts {
  archives deobfJar
}

